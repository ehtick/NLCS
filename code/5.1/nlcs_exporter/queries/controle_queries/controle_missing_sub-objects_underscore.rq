PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX sh: <http://www.w3.org/ns/shacl#>

# Check if a sub object is missing in the hierarchy

SELECT ?hoofdgroep ?object ?parent WHERE {
  ?NLCSObjectTopConcept a owl:Class .
  ?NLCSObjectTopConcept skos:prefLabel "NLCS-object" .
  ?hoofdgroepURI rdfs:subClassOf ?NLCSObjectTopConcept .
  ?hoofdgroepURI skos:prefLabel ?hoofdgroep .
  ?objectURI rdfs:subClassOf+ ?hoofdgroepURI .
  ?objectURI skos:prefLabel ?object .
  BIND (strbefore(?object, "-")  AS ?objectName)

  ?objectURI rdfs:subClassOf ?parentURI .
  ?parentURI skos:prefLabel ?parent .
  BIND (strbefore(?parent, "-")  AS ?parentName)

  BIND((strlen(?objectName) - strlen(replace(?objectName, "_", ""))) AS ?object_underscore_count ) .
  BIND((strlen(?parentName) - strlen(replace(?parentName, "_", ""))) AS ?parent_underscore_count ) .
  FILTER(contains(?objectName, "_") && (?object_underscore_count != ?parent_underscore_count + 1)) .
} ORDER BY ?hoofdgroep